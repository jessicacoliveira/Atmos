val sdl = require "atmos.env.sdl"
val SDL = require "SDL"

val w_screen, h_screen = (800, 600)
val rect_max = 10

math.randomseed()

;; sdl.window equivale ao SDL_CreateWindow + SDL_CreateRenderer
val _, REN = sdl.window @{
	title  = "Ex. 1.4.1 - Atmos",
	width = w_screen,
	height = h_screen,
	flags = @{ SDL.flags.OpenGL },
} --> assert

func check_lim (min, max, num){
	ifs{
		num > max => max
		num < min => min
		else	=> num
	}
}

;; Troca a cor de fundo
spawn {
	val color = @{r=255,g=255,b=255}
	every :sdl.draw{
		REN::setDrawColor(color)
		REN::clear()
		
	}
}

func Big_Square (x0, y0){
	val rect_size = 100
	val rect = @{ x = x0, y = y0, w = rect_size, h = rect_size }
	val color = @{r=0,g=0,b=255}

	par {
		every it in SDL.event.KeyDown {
			match it.name{
				'Up' => set rect.y = check_lim (0, h_screen - rect.h, rect.y - 5)
				'Down' => set rect.y = check_lim (0, h_screen - rect.h, rect.y + 5)
				'Left' => set rect.x = check_lim (0, w_screen - rect.w, rect.x - 5)
				'Right' => set rect.x = check_lim (0, w_screen - rect.w, rect.x + 5)
			}
		}
	} with {
	
		every :sdl.draw{
			REN::setDrawColor(color)
			REN::fillRect(sdl.ints @{ x=rect.x, y=rect.y, w=rect.w, h=rect.h })
		}
	}
}

func Random_Square (x0, y0){
	val rect_w, rect_h = (math.random(0,100), math.random(0,100))
	val rect = @{ x = x0,
		      y = y0,
		      w = check_lim (0, w_screen - rect_w, rect_w),
		      h = check_lim (0, h_screen - rect_h, rect_h),
		     }
	val color = @{ r = math.random(0,150), g = math.random(0,150), b = math.random(0,150)}
		     
	every :sdl.draw{
		REN::setDrawColor(color)
		REN::fillRect(sdl.ints @{ x=rect.x, y=rect.y, w=rect.w, h=rect.h })
	}
}

spawn Big_Square (40,20)
pin random_squares = tasks (rect_max)
every it in SDL.event.MouseButtonDown {
		spawn [random_squares] Random_Square(it.x, it.y)
}
